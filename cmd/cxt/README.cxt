[{h1}cxt: text markup for civilization]
["[*Note:] This file is created using cxt. The source is at
  [$./README.cxt] ]

It should not take more than a few minutes to know how to read and write
documentation.

cxt is an ultra-simple markup language which is designed to be as easy as
possible to parse (for computers [,and] humans) while still using as few
characters as possible to express format for common cases.

cxt is designed to: [+
 * Make document writing easy.
 * Make document parsing easy.
 * Make reading of raw (text) documents easy.
 * Make consuming structured data from documents easy.
]
CXT contains only three special characters for deliniating formatting controls: [+
* the two bracket characters [$[]]
* a way to escape brackets and itself [$\]
]

There are two ways to specify attributes for a block: [+
* short: [$[*bold text]] where [$*] can be any of the shortcuts below
* full: [$[{*, attr1 attr2=value}bold italic with attr 1 and 2]]
]

The following attributes and shortcuts are supported
[###
  b i u   bold (*), italic (,) and underlined (_)
  * , _   Example: [*bold] [,italic] [_underlined]
                   [{*,_}bold italic and underlined]

  code    inline or block code (depending on whether newlines exist)
   $      shortcut: [$inline code]
   #...   See also: '[#... ]#...' which sets the code attribute.

  quote   create a quote block
   "      Example: ["quote block]

  list    list syntax, see demos
   +      Example: [+
            * list item 1
            * list item 2
          ]

  table   table syntax, see demos.
  # head  modify the delimiters with row/col/head attrs.
  + row
  | col

  hidden  hidden block, used for comments and/or assigning attributes
   !      Example: [!hidden block]

  #...    any number of # characters create a "raw" block that must
          have the same number of # characters closing it.

          [##
          some code
          ]##

          [{## code=html}
          <b>example html</b>
          ]##

  href    hyper reference, aka a url.
  <href>

   /      a path, typically relative to project or website root.
          Example: [/path/to/file.lua]

  name    sets the name of the block.
   n      Used with: [@name]  href=#name  anyAttr=@name
   :      shortcut: [{: ...}name]
          Example: [{name=Civboot href=http://civboot.org}Civboot]
          Example: [{: href=http://civboot.org}Civboot]

  value   when @ is used for an attr value, it will get this attribute.
   v      defaults to "href" if the node has one, else "text" which
          is the node's text.

   @      copies a block with given name. When used as an attr value
          it gets the attribute referenced by "value"
          Example:
            [{!:href=http://civboot.org}Civboot] creates a hidden named block
            [@Civboot] clones the block (removing name and hidden)
            [<@Civboot>Civboot Project] has href=http://civboot.org
              note: the default for "value" is "href" if it exists.

 escapes
   \[    literal [
   \]    literal ]
   \\    literal \
   \*    literal \*, where * is any other character
]###

[" Note:
   Any value after a bracket besides [$ {...} b i u * , _ $ " + ! / : v @ ]
   is invalid.]

[{:h2}Demos]

[{h3}Inline Code:]

[{# lang=cxt}[$inline code]. Note that [$balanced[brackets] are allowed]]# [+
* [$inline code]. Note that [$balanced[brackets] are allowed]
]

[{h3}Code block:]

Writing this: [{### lang=cxt}
[{## lang=lua}
function example()
  print'Hello world'
end
]##
]###

Will create a formatted code block: [{## lang=lua}
function example()
  print'Hello world'
end
]##

[{h3}Text Formatting]
[{# lang=cxt}[*bold], [,italic] and [_underlined]]# [+
* [*bold], [,italic] and [_underlined]
]

[{h3}Linking]
[{# lang=cxt}[{b i href=http://civboot.org}bold italic link]]# [+
* [{b i href=http://civboot.org}bold italic link]
]

[{h3}List]
Lists are started with [$+], each items is deliniated by [$*]: [{### lang=cxt}
A list of stuff: [+
* example list
* with sub list:[+
  * sub item
  * multi-line

    sub item.
  * inner code block
    [##
    function example()
      print'example inner block'
    end
    ]##
  * final sub
]
* final item
]
]###

A list of stuff: [+
* example list
* with sub list:[+
  * sub item
  * multi-line

    sub item.
  * inner code block
    [##
    function example()
      print'example inner block'
    end
    ]##
  * final sub
]
* final item
]

[{h3}Hidden]
[{# lang=cxt}Hidden block: "[! this is hidden]"]# [+
* Hidden block: "[! this is hidden]"
]

[{h3}Table]
A table is created by the 'table' attribute. We also set the [$head], [$row]
and [$col] to the defaults for demonstration (they can be excluded). The
[$row] and [$head] deliminters are only recognized at the start of a line.

[{## lang=cxt}
A table: [{table head=# row=+ col=|}
# col 1      | col 2   | col 3
+ row [*1.1] | row 1.2 | row 1.3
+ row [*1.1] | row 1.2 | row 1.3
]
]##

A table: [{table head=# row=+ col=|}
# col 1      | col 2   | col 3
+ row [*1.1] | row 1.2 | row 1.3
+ row [*1.1] | row 1.2 | row 1.3
]

[{h3}Table with large columns]
For very large or multi-lined columns, you can organize them similar to
sub-lists: [###
[{table}
# 1st header column
| 2nd header column
| 3rd header column
+ long row 1.1
| long row 1.2

  Even includes new line!
| long row 1.3
  [{## code=lua}
  function example()
    return "code block"
  end
  ]##
+ row 2.1
| row 2.2
| row 2.3
]
]###

[{table}
# 1st header column
| 2nd header column
| 3rd header column
+ long row 1.1
| long row 1.2

  Even includes new line!
| long row 1.3
  [{## code=lua}
  function example()
    return "code block"
  end
  ]##
+ row 2.1
| row 2.2
| row 2.3
]

[{h3}Named Nodes and Values]
[" [*Note]: all visible named blocks have a ‚òç link next to them and serve dual
   purpose of being referenced within the cxt file using [$[@name]] and by
   local and external links ([$href=#name]) ]

[$A hidden named block: "[{!:href=http://civboot.org}Civboot]"] [+
* A hidden named block: "[{!:href=http://civboot.org}Civboot]"
]

[$A visible named block: [{:href=https://github.com/civboot/civlua}civlua]] [+
* A visible named block: [{:href=https://github.com/civboot/civlua}civlua]
]

[$[@Civboot] clones the hidden block (removing the [$clone] attribute)] [+
* [@Civboot] clones the hidden block (removing the [$clone] attribute)
]

[$[<@Civboot>Civboot Project] has [$href=http://civboot.org].][+
* [<@Civboot>Civboot Project] has [$href=http://civboot.org].
]

[##
You can also link to named blocks with [$href=#name], for instance the "Demos"
header is named [<#Demos>Demos].
]## [+
* You can also link to named blocks with [$href=#name], for instance the "Demos"
  header is named [<#Demos>Demos].
]

[{h3}Escaping \[brackets\]]
[##
You can \[escape\] brackets to render them directly. You can also escape '\\'.
]## [+
* You can \[escape\] brackets to render them directly. You can also escape '\\'.
]

[{h2}UNLICENSE]
Cxt is in the the public domain, see the [<../UNLICENSE>UNLICENSE] for details.

It was originally created by Rett Berg as part of the [@Civboot] project.
